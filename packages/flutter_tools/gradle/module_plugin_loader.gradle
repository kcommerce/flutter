// Copyright 2014 The Flutter Authors. All rights reserved.
// Use of this source code is governed by a BSD-style license that can be
// found in the LICENSE file.

// This file is included from `<module>/.android/include_flutter.groovy`,
// so it can be versioned with the Flutter SDK.

import java.nio.file.Paths

final File pathToThisDirectory = buildscript.sourceFile.parentFile
apply from: Paths.get(pathToThisDirectory.absolutePath, 'src', 'main', 'groovy', 'native_plugin_loader.groovy')

final def moduleProjectRoot = project(':flutter').projectDir.parentFile.parentFile

final List<Map<String, Object>> nativePlugins = nativePluginLoader.getPlugins(moduleProjectRoot)
nativePlugins.each { final androidPlugin ->
    final def pluginDirectory = new File(androidPlugin.path as String, 'android')
    assert pluginDirectory.exists()
    include ":${androidPlugin.name}"
    project(":${androidPlugin.name}").projectDir = pluginDirectory
}

final String flutterModulePath = project(':flutter').projectDir.parentFile.getAbsolutePath()
gradle.projectsLoaded { final Gradle gradleInstance ->
    gradleInstance.rootProject.beforeEvaluate { final Project project ->
        project.subprojects { final Project subproject ->
            if (nativePlugins.name.contains(subproject.name)) {
                final File androidPluginBuildOutputDir = new File(flutterModulePath + File.separator
                        + 'plugins_build_output' + File.separator + subproject.name)
                if (!androidPluginBuildOutputDir.exists()) {
                    androidPluginBuildOutputDir.mkdirs()
                }
                subproject.layout.buildDirectory.fileValue(androidPluginBuildOutputDir)
            }
        }
        final def _mainModuleName = binding.variables['mainModuleName']
        if (_mainModuleName != null && !_mainModuleName.empty) {
            project.ext.mainModuleName = _mainModuleName
        }
    }
    gradleInstance.rootProject.afterEvaluate { final Project project ->
        project.subprojects { final Project subproject ->
            if (subproject.name != 'flutter') {
                subproject.evaluationDependsOn(':flutter')
            }
        }
    }
}
